plugins {
    id 'java-library'
    id 'maven-publish'
    id 'checkstyle'
    id 'jacoco'
    id 'pmd'
    id 'com.jfrog.artifactory' version '4.24.18'
}

repositories {
    maven {
        url 'https://artifactory.cloud.cms.gov/artifactory/ab2d-maven-repo'
        credentials {
            username = "${artifactory_user}"
            password = "${artifactory_password}"
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

group 'gov.cms.ab2d'
version '1.0-RELEASE'
sourceCompatibility = 11
targetCompatibility = 11

ext {
    hapiVersion = '5.4.0'
}

dependencies {
    implementation 'org.projectlombok:lombok:1.18.20'
    implementation "ca.uhn.hapi.fhir:hapi-fhir-base:$hapiVersion"
    implementation "ca.uhn.hapi.fhir:hapi-fhir-client:$hapiVersion"
    implementation "ca.uhn.hapi.fhir:org.hl7.fhir.r4:$hapiVersion"
    implementation "ca.uhn.hapi.fhir:org.hl7.fhir.dstu3:$hapiVersion"
    implementation "ca.uhn.hapi.fhir:hapi-fhir-structures-dstu3:$hapiVersion"
    implementation "ca.uhn.hapi.fhir:hapi-fhir-structures-r4:$hapiVersion"
    implementation 'com.github.spotbugs:spotbugs-annotations:4.2.0'

    annotationProcessor "org.projectlombok:lombok:1.18.16"

    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.0'
    testImplementation 'com.github.spotbugs:spotbugs-annotations:4.2.3'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}

test {
    testLogging {
        showStandardStreams true
        events "passed", "skipped", "failed"
    }
    useJUnitPlatform()
    finalizedBy jacocoTestReport // report is always generated after tests run
}

jacocoTestReport {
    dependsOn test
    dependsOn jacocoTestCoverageVerification
}

jacoco {
    toolVersion = "0.8.7"
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.80
            }
        }
    }
}

artifactory {
    contextUrl = "${artifactory_contextUrl}"   //The base Artifactory URL if not overridden by the publisher/resolver

    publish {

        repository {
            repoKey = 'ab2d-test'
            username = "${artifactory_user}"
            password = "${artifactory_password}"
            maven = true

        }
        defaults {
            publications('mavenJava')
            publishArtifacts = true
            publishBuildInfo = false
        }
    }
    resolve {
        repository {
            repoKey = 'ab2d-test'
            username = "${artifactory_user}"
            password = "${artifactory_password}"
            maven = true

        }
    }
}