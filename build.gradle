import groovy.time.TimeCategory
import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent

plugins {
    id 'java-library'
    id 'com.github.spotbugs' version '4.7.6' apply false
    id 'com.jfrog.artifactory' version '4.24.18' apply false
    id "org.sonarqube" version "3.0"
}

ext {
    artifactoryLoc = project.hasProperty('artifactory_contextUrl') ? project.artifactory_contextUrl
            : System.getenv()['ARTIFACTORY_URL']

    // Override user and password
    artifactory_user = project.hasProperty('artifactory_user') ? project.artifactory_user
            : System.getenv()['ARTIFACTORY_USER']
    artifactory_password = project.hasProperty('artifactory_password') ? project.artifactory_password
            : System.getenv()['ARTIFACTORY_PASSWORD']

    fhirVersion='1.0'
    bfdVersion='1.0.1'
    filtersVersion='1.2'


    sourcesRepo = 'ab2d-maven-repo'
    deployerRepo = 'ab2d-main'
    resolverRepo = 'ab2d-main'

    lombokVersion = '1.18.20'
    hapiVersion = '5.6.1'
    spotbugsVersion = '4.2.0'
    junitVersion = '5.7.0'
    springBootVersion='2.6.2'
    springframeworkRetryVersion='1.3.1'
    httpComponentsVersion='4.5.13'
    hapiFhirVersion='5.6.1'
    newRelicVersion='7.4.2'
    testContainerVersion='1.16.2'
    mockServerVersion='5.11.2'



    failedTests = []
    skippedTests = []
    testSuites = []
}

allprojects {

    tasks.withType(Test) { testTask ->

        testTask.testLogging { logging ->
            events TestLogEvent.FAILED,
                    TestLogEvent.SKIPPED,
                    TestLogEvent.STANDARD_OUT,
                    TestLogEvent.STANDARD_ERROR
            exceptionFormat TestExceptionFormat.FULL
            showExceptions true
            showCauses true
            showStackTraces true
        }

        afterTest { desc, result ->
            if (result.resultType == TestResult.ResultType.FAILURE) {
                rootProject.ext.failedTests.add(desc)
            }

            if (result.resultType == TestResult.ResultType.SKIPPED) {
                rootProject.ext.skippedTests.add(desc)
            }
        }

        afterSuite { desc, result ->
            if (desc.parent) return // Only summarize results for whole modules

            String summary = "${testTask.project.name}:${testTask.name} results: ${result.resultType} " +
                    "(" +
                    "Tests run ${result.testCount}, " +
                    "succeeded ${result.successfulTestCount}, " +
                    "failed ${result.failedTestCount}, " +
                    "skipped ${result.skippedTestCount}" +
                    ") "
            rootProject.ext.testSuites += summary
        }
    }


    // In each Project run to check if the version exists in the repository
    task lookForArtifacts {
        doLast {
            //This is the parent build where nothing gets published. Skip it.
            if(project.name != 'ab2d-libs')
                //Set path to repository that might exist if previous published on this version.
                //Sets project name and if it exists in repository. Print out so jenkins can take it.
                System.out.print("'''"+project.name + ":" + urlExists("${artifactoryLoc}/${deployerRepo}/gov/cms/ab2d/${project.name}/${project.version}/${project.name}-${project.version}.jar"))
        }
    }


    repositories {
        maven {
            url = "${artifactoryLoc}/${sourcesRepo}"
            credentials {
                username = project.artifactory_user
                password = project.artifactory_password
            }
        }
    }
}

subprojects {
    apply plugin: 'com.jfrog.artifactory'
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'checkstyle'
    apply plugin: 'jacoco'
    apply plugin: 'pmd'
    apply plugin: 'com.github.spotbugs'

    if (artifactory_user == null || artifactory_user.isEmpty()) {
        println("Artifactory user not set");
    }
    if (artifactory_password == null || artifactory_password.isEmpty()) {
        println("Artifactory password not set");
    }
    println("Artifactory loc? " + project.getProperty('artifactoryLoc'))

    println("Branch is " + gitBranch())

    sourceCompatibility = 11
    targetCompatibility = 11

    group 'gov.cms.ab2d'

    dependencies {
        compileOnly "org.springframework.boot:spring-boot-dependencies:$springBootVersion@pom"
        implementation "com.github.spotbugs:spotbugs-annotations:$spotbugsVersion"

        testImplementation "org.springframework.boot:spring-boot-starter-test:$springBootVersion"

        testImplementation "org.junit.jupiter:junit-jupiter:$junitVersion"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
    }

    jar {
        processResources.exclude('checkstyle.xml')
        processResources.exclude('spotbugs-filter.xml')
        classifier "main".equalsIgnoreCase(gitBranch()) || "main".equalsIgnoreCase(System.getenv('BRANCH_NAME')) ? "" : "SNAPSHOT"
        out.println("**** building branch - " + gitBranch() + ", classifier - " + classifier + " - CI branch - " + System.getenv('BRANCH_NAME'))
    }

    test {
        useJUnitPlatform()
        finalizedBy jacocoTestReport // report is always generated after tests run
    }

    jacocoTestReport {
        dependsOn test
        dependsOn jacocoTestCoverageVerification
    }

    jacoco {
        toolVersion = "0.8.7"
    }

    jacocoTestCoverageVerification {
        violationRules {
            rule {
                limit {
                    minimum = 0.80
                }
            }
        }
    }

    checkstyle {
        configFile file("$rootDir/config/checkstyle.xml")
    }

    spotbugs {
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }
    }

    artifactory {
        contextUrl = project.artifactoryLoc

        publish {
            repository {
                repoKey = "${deployerRepo}"
                username = project.artifactory_user
                password = project.artifactory_password
                maven = true
            }
            defaults {
                publications('mavenJava')
                publishArtifacts = true
                publishBuildInfo = false
            }
        }
        resolve {
            repository {
                repoKey = "${resolverRepo}"
                username = project.artifactory_user
                password = project.artifactory_password
                maven = true
            }
        }
    }
}

gradle.buildFinished {

    if (!rootProject.ext.skippedTests.isEmpty()) {
        println "Skipped Tests: "
        for (String skippedTestDesc : rootProject.ext.skippedTests) {
            println "\t" + skippedTestDesc
        }
    }

    if (!rootProject.ext.failedTests.isEmpty()) {
        println "Failing Tests: "
        for (String failedTestDesc : rootProject.ext.failedTests) {
            println "\t" + failedTestDesc
        }
    }

    if (!rootProject.ext.testSuites.isEmpty()) {
        println "Test Suite Summary: "
        println rootProject.ext.testSuites
    }
}

def gitBranch() {
    def branch = ""
    def proc = "git rev-parse --abbrev-ref HEAD".execute()
    proc.in.eachLine { line -> branch = line }
    proc.err.eachLine { line -> println line }
    proc.waitFor()
    branch
}


def getBase64EncodedCredentials() {
    def s = "$artifactory_user" + ":" + "$artifactory_password"
    return s.bytes.encodeBase64().toString()
}


//Check if url exist in the repository
def urlExists(repositoryUrl) {

    try {
        def connection = (HttpURLConnection) new URL(repositoryUrl).openConnection()

        connection.setRequestProperty("Authorization", "Basic " + getBase64EncodedCredentials())
        connection.setConnectTimeout(10000)
        connection.setReadTimeout(10000)
        connection.setRequestMethod("HEAD")

        def responseCode = connection.getResponseCode()

        if (responseCode == 401) {
            throw new RuntimeException("Unauthorized MavenUser user. Please provide valid username and password.")
        }

        return (200 == responseCode)

    } catch (IOException ignored) {
        println(ignored)
        return false
    }

}
